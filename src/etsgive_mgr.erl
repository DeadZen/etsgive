-module(etsgive_mgr).

-behaviour(gen_server).

%% API
-export([start_link/0]).

%% gen_server callbacks
-export([init/1,
         handle_call/3,
         handle_cast/2,
         handle_info/2,
         terminate/2,
         code_change/3]).

-define(SERVER, ?MODULE).

-record(state, {table_id}).

%%%===================================================================
%%% API
%%%===================================================================

gift() ->
    gen_server:cast(?MODULE, {gift, {count, 0}}).

%%--------------------------------------------------------------------
%% @doc
%% Starts the server
%%
%% @spec start_link() -> {ok, Pid} | ignore | {error, Error}
%% @end
%%--------------------------------------------------------------------
start_link() ->
        gen_server:start_link({local, ?SERVER}, ?MODULE, [], []).

%%%===================================================================
%%% gen_server callbacks
%%%===================================================================

%%--------------------------------------------------------------------
%% @private
%% @doc
%% Initializes the server
%%
%% @spec init(Args) -> {ok, State} |
%%                     {ok, State, Timeout} |
%%                     ignore |
%%                     {stop, Reason}
%% @end
%%--------------------------------------------------------------------
init([]) ->
    process_flag(trap_exit, true),
    gift(),
    {ok, #state{}}.

%%--------------------------------------------------------------------
%% @private
%% @doc
%% Handling call messages
%%
%% @spec handle_call(Request, From, State) ->
%%                                   {reply, Reply, State} |
%%                                   {reply, Reply, State, Timeout} |
%%                                   {noreply, State} |
%%                                   {noreply, State, Timeout} |
%%                                   {stop, Reason, Reply, State} |
%%                                   {stop, Reason, State}
%% @end
%%--------------------------------------------------------------------
handle_call(_Request, _From, State) ->
        Reply = ok,
        {reply, Reply, State}.

%%--------------------------------------------------------------------
%% @private
%% @doc
%% Handling cast messages
%%
%% @spec handle_cast(Msg, State) -> {noreply, State} |
%%                                  {noreply, State, Timeout} |
%%                                  {stop, Reason, State}
%% @end
%%--------------------------------------------------------------------
handle_cast({gift, Data}, State) ->
        TableId = ets:new(?MODULE, [private]),
        ets:insert(TableId, Data),
        ets:setopts(TableId, {heir, self(), Data}),
        giveaway_table(TableId, Data),
        {noreply, State#state{table_id=TableId}};
handle_cast(_Msg, State) ->
        {noreply, State}.

giveaway_table(TableId, Data) ->
    try
        SRV = wait_for_srv(),
        link(SRV),
        ets:give_away(TableId, SRV, Data)
    catch K:V ->
        io:format("Problem giving ets table away ~p ~p", [K, V]),
        giveaway_table(TableId, Data)
    end.

%%--------------------------------------------------------------------
%% @private
%% @doc
%% Handling all non call/cast messages
%%
%% @spec handle_info(Info, State) -> {noreply, State} |
%%                                   {noreply, State, Timeout} |
%%                                   {stop, Reason, State}
%% @end
%%--------------------------------------------------------------------
handle_info({'EXIT',Pid,killed}, State) ->
    TableId = State#state.table_id,
    io:format("SRV(~p) !! is now dead, farewell TableId: ~p~n", [Pid, TableId]),
    {noreply, State};
handle_info({'ETS-TRANSFER', TableId, Pid, Data}, State) ->
    io:format("Warning TableId: ~p OwnerPid: ~p is dying~n"
              "SRV(~p) => MGR(~p) handing TableId: ~p~n", [TableId, Pid, Pid, self(), TableId]),
    giveaway_table(TableId, Data),
    {noreply, State#state{table_id=TableId}}.

wait_for_srv() -> 
    case whereis(etsgive_srv) of
        undefined -> 
            timer:sleep(1),
            wait_for_srv();
        Pid -> Pid
    end.



%%--------------------------------------------------------------------
%% @private
%% @doc
%% This function is called by a gen_server when it is about to
%% terminate. It should be the opposite of Module:init/1 and do any
%% necessary cleaning up. When it returns, the gen_server terminates
%% with Reason. The return value is ignored.
%%
%% @spec terminate(Reason, State) -> void()
%% @end
%%--------------------------------------------------------------------
terminate(_Reason, _State) ->
        ok.

%%--------------------------------------------------------------------
%% @private
%% @doc
%% Convert process state when code is changed
%%
%% @spec code_change(OldVsn, State, Extra) -> {ok, NewState}
%% @end
%%--------------------------------------------------------------------
code_change(_OldVsn, State, _Extra) ->
        {ok, State}.

%%%===================================================================
%%% Internal functions
%%%===================================================================



